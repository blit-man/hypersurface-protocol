// SPDX-License-Identifier: GPL-3.0

pragma solidity ^0.8.0;

import 'openzeppelin-contracts/contracts/access/Ownable.sol';

import '../../Interface/IHolderClaimsRequired.sol';

contract HolderRequiredClaims is IHolderClaimsRequired, Ownable {
    
    /// @dev All required Claim Topics
    uint256[] private claimTopics;

    /**
     *  @dev See {IHolderClaimsRequired-addClaimTopic}.
     */
    function addClaimTopic(uint256 _claimTopic) external override onlyOwner {
        uint256 length = claimTopics.length;
        for (uint256 i = 0; i < length; i++) {
            require(claimTopics[i] != _claimTopic, 'claimTopic already exists');
        }
        claimTopics.push(_claimTopic);
        emit ClaimTopicAdded(_claimTopic);
    }

    /**
     *  @dev See {IHolderClaimsRequired-removeClaimTopic}.
     */
    function removeClaimTopic(uint256 _claimTopic) external override onlyOwner {
        uint256 length = claimTopics.length;
        for (uint256 i = 0; i < length; i++) {
            if (claimTopics[i] == _claimTopic) {
                claimTopics[i] = claimTopics[length - 1];
                claimTopics.pop();
                emit ClaimTopicRemoved(_claimTopic);
                break;
            }
        }
    }

    /**
     *  @dev See {IHolderClaimsRequired-getClaimTopics}.
     */
    function getClaimTopics() external view override returns (uint256[] memory) {
        return claimTopics;
    }

    /**
     *  @dev See {IHolderClaimsRequired-transferOwnershipOnHolderRequiredClaimsContract}.
     */
    function transferOwnershipOnHolderRequiredClaimsContract(address _newOwner) external override onlyOwner {
        transferOwnership(_newOwner);
    }
}
